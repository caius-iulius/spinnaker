use Core

mod Monad "monad.spk"

data pub Maybe a = None | Some a

def pub isSome : forall a. Maybe a -> Bool =
    \ None -> False
    | Some _ -> True

def pub isNone : forall a. Maybe a -> Bool =
    \ None -> True
    | Some _ -> False

def pub fromSome : forall a. Maybe a -> a
    = \Some a -> a

def pub maybe : forall a b. b -> (a -> b) -> Maybe a -> b
    = \b, f, m -> put m
    | None -> b
    | Some a -> f a

def pub fromMaybe : forall a. a -> Maybe a -> a
  = \a -> maybe a id

inst forall a. {Show a} => Show (Maybe a) { #NOTE: temporaneo, aggiunge parentesi di troppo
    def show =
        \ None -> "None"
        | Some a -> "Some (" ++ show a ++ ")"
}

inst Functor Maybe {
    def fmap = \f, m -> put m
        | None -> None
        | Some a -> Some (f a)
}
inst Monad Maybe {
    def return = \a -> Some a
    def bind = \m, f -> put m
        | None -> None
        | Some a -> f a
}

inst Monad.MonadPlus Maybe {
    def mzero = None
    def mplus = \a, b -> put a
        | None -> b
        | Some _ -> a
}

inst Monad.MonadFail Maybe { def fail = \_ -> None }
