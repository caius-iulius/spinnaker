use Core

def mapMAux = \procd, f, l -> put l
    | [] -> return (reverse procd)
    | [x | xs] ->
        a <- f x;
        mapMAux (a :: procd) f xs
def pub mapM : forall m a b. {Monad m} => (a -> m b) -> [a] -> m [b]
    = mapMAux []

def pub mapM_ : forall m a b. {Monad m} => (a -> m b) -> [a] -> m () =
    \ _, [] -> return ()
    | f, [x | xs] -> f x >> mapM_ f xs

rel pub {Monad m} => MonadPlus m =
    # LAWS
    # 1) MONOID
    # 1.1) mplus mzero a = a
    # 1.2) mplus a mzero = a
    # 1.3) mplus (mplus a b) c = mplus a (mplus b c)
    # 2) mzero >>= f = mzero
    # 3) m >> mzero = mzero
    mzero : forall a. m a,
    mplus : forall a. m a -> m a -> m a

def pub guard : forall m. {MonadPlus m} => Bool -> m () =
    \ True -> return ()
    | False -> mzero

inst MonadPlus [] {
    def mzero = []
    def mplus = (++)
}

rel pub {Monad m} => MonadFail m =
    # Se {MonadPlus m} si può considerare: mzero = fail "mzero"
    fail : forall a. String -> m a

inst MonadFail IO {
    def fail = error
}

rel pub MonadTrans t = lift : forall m a. {Monad m, Monad (t m)} => m a -> t m a
    # LAWS
    # 1) lift <| return = return
    # 2) lift (m >>= f) = lift m >>= (lift <| f)

# Monade identità, può servire come placeholder nei transformers, ad esempio State s a = StateT s Id a
mod pub Id {
    data pub Id a = Id a
    def pub runId : forall a. Id a -> a = \Id a -> a
    
    inst Functor Id {
        def fmap = \f, Id a -> Id (f a)
    }
    inst Monad Id {
        def return = Id
        def bind = \Id a, f -> f a
    }
}

mod pub State {
    # qui la mancanza delle FunctionalDependencies è un problema, in diversi casi l'inferenza è indecidibile
    # rel pub {Monad m} => MonadState m s =
    #     gets : m s,
    #     puts : s -> m ()

    data pub StateT s m a = StateT (s -> m (a, s))
    def pub runStateT : forall s m a. StateT s m a -> s -> m (a, s)
        = \StateT sf -> sf

    inst forall s f. {Functor f} => Functor (StateT s f) {
        def fmap = \f, StateT sf -> StateT (\s ->
            fmap (\(a, s') -> (f a, s')) (sf s) )
    }

    inst forall s m. {Monad m} => Monad (StateT s m) {
        def return = \a -> StateT (\s -> return (a, s))
        def bind = \StateT sf, f -> StateT (\s ->
            (a, s') <- sf s;
            let StateT sf' = f a ->
            sf' s'
        )
    }

    # inst forall s m. {Monad m} => MonadState (StateT s m) s {
        def pub gets = StateT (\s -> return (s, s))
        def pub puts = \s -> StateT(const $ return ((), s))
    # }

    inst forall s. MonadTrans (StateT s) {
        def lift = \m -> StateT (\s -> fmap (\a -> (a, s)) m)
    }

    typesyn pub State s = StateT s Id.Id
    def pub runState : forall s a. (State s) a -> s -> (a, s) = \m, s -> Id.runId (runStateT m s)
}
